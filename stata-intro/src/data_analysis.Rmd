---
title: "Data Analysis"
author: "Data Team, [Center on Budget and Policy Priorities](https://www.cbpp.org)"
date: "Spring 2021"
output: 
  html_document:
    theme: paper
    toc: true
    toc_depth: 1
---

```{r initialize-statamarkdown, echo=FALSE, message=FALSE}
#install.packages("usethis")
#install.packages("rlang")
#install.packages("devtools")
#devtools::install_github("hadley/devtools")
#devtools::install_github("Hemken/Statamarkdown")
library(Statamarkdown)
```


```{r setup, include=FALSE}
knitr::opts_chunk$set(collectcode=TRUE, #include=TRUE,
                      # comment out the line below to execute Stata code
                      # eval=FALSE, 
                      savedo=TRUE)
                      
options(knitr.kable.NA = "")
```

```{stata render-setup, include=FALSE, savedo=FALSE}
set rmsg off
set more off
set linesize 100
```
```{r include=FALSE}
colorize <- function(x, color) {
  if (knitr::is_latex_output()) {
    sprintf("\\textcolor{%s}{%s}", color, x)
  } else if (knitr::is_html_output()) {
    sprintf("<span style='color: %s;'>%s</span>", color, 
      x)
  } else x
}
```

In this project, we will calculate the state's per-student education appropriations over time. [Here](https://github.com/CenterOnBudget/r-intro-training/blob/3d6aede99d18e31c55e9db55d638f777abb20876/scripts/higher_ed_trends.R) you can also find the same project done using R.

# Set environment 

```{stata, results="hide"}
clear
set more off
set matsize 11000
```

Let's start with setting the working directory:


```{stata eval=FALSE, echo = c(6)}
/* 
* Set working directory ----------------------

* Uncomment the line below and define your working folder

cd "C:/Users/my_username/Documents/project_folder/"
*/
```

Next, we create the file `higher_ed.smcl` to keep a record of our work:

```{stata, results="hide"}
log using higher_ed, replace
```

# Retrieve data

Every Stata command has syntax: what you type, and in what order, to tell the command what you want to do. Each Stata command has a help file that can be accessed by typing `help` and then the name of the command. For instance, if you want to learn about the `copy` command -- which copies files from disks or URLs, run: 

```{stata, eval=FALSE}
help copy
```

Using the `copy` command, we will download state higher education funding data from SHEEO and inflation data from the BLS and save these two files to the "*data-raw*" folder in the `wd`[^1].

[^1]: Short for working directory

```{stata, echo = c(2,3,4,6,7), results="hide"}
* Download state higher education funding data from SHEEO.
copy "https://shef.sheeo.org/wp-content/uploads/2020/04/SHEEO_SHEF_FY19_Report_Data.xlsx"	///
	 "data-raw/sheeo_shef_fy19_data.xlsx", replace

* Download inflation data from the BLS.
copy "https://www.bls.gov/cpi/research-series/allitems.xlsx"	///
	 "data-raw/bls_cpi_u_rs.xlsx", replace
```

After we saved the raw data, we will import it in Stata starting with the inflation dataset.

```{stata, echo = c(4), results="hide"}
* Import inflation data -----------------
help import excel 

import excel "data-raw/bls_cpi_u_rs.xlsx", sheet("Table 1") cellrange(A6:N49) firstrow case(lower) clear
```

# Clean data

Keep only the variables we will need for this analysis and check for missing values:

```{stata echo = c(3, 4)}
* Keep only the variables we will need for this analysis and check for missing values
  
keep year avg
codebook year avg
```

As data `avg` for the year of *1977* is missing, we will drop this row:

```{stata}
drop if year == 1977
```

Rename the `avg` variable to a more descriptive name:

```{stata echo = c(3),  results="hide"}
* Rename the avg variable to a more descriptive name

rename avg cpi_u_rs
```

Next, we will calculate a new variable, an inflation adjustment factor to 2019 dollars. Later, we'll join this variable into the SHEEO data and use it to adjust the dollar amounts for inflation. To do this, we'll first use the `egen` command to create a new variable that is equal to the maximum value of `cpi_u_rs` in our dataset. The Stata command `egen`, which stands for *extended generation*, is used to create variables that require some additional function in order to be generated (e.g., *mean, max, min*).

```{stata results="hide"}
egen cpi_u_rs_2019 = max(cpi_u_rs)
```

><small> The row with the maximum value of `cpi_u_rs` is the 2019 row because the CPI-U-RS almost always increases every year.</small>

After that, we will create the inflation adjustment factor using the `generate` command.

```{stata echo = c(3),  results="hide"}
* Next, create the inflation adjustment factor.

generate cpi_u_rs_2019_adj = cpi_u_rs_2019 / cpi_u_rs
```

Again, keep only the variables we will need.

```{stata echo = c(3), results="hide"}
* Again, keep only the variables we will need.

keep year cpi_u_rs_2019_adj
```

The SHEEO dataset goes back to 1980, but the inflation dataset goes back to 1977. Let's keep only the rows in the dataset where the year is 1980 or later.

```{stata echo = c(3), results="hide"}
* Keep only the rows in the dataset where the year is 1980 or later.

keep if year >= 1980
```

Next, we will export inflation data. We will join it with the SHEEO dataset later.

```{stata echo = c(3), results="hide"}
* Save our cleaned inflation dataset. 

save "data/inflation.dta" , replace
```

# Merge two datasets

Import SHEEO data:

```{stata echo = c(3,4,5), results="hide"}
* Import SHEEO data --------------
  
import excel "data-raw\sheeo_shef_fy19_data.xlsx", 	///
	   sheet("SHEF Report Data") 					///
	   firstrow case(lower) clear
```


Keep only the variables we will need for this analysis.

```{stata echo = c(3), results="hide"}
* Keep only the variables we will need 

keep state fy totalstatesupport netfteenrollment
summarize
```

Rename the variables to shorter names.

```{stata echo = c(3, 4), results="hide"}
* Rename the variables to shorter names.

rename totalstatesupport support
rename netfteenrollment fte
```

To merge datasets, they must share a variable with the same name. Let's rename `fy` to `year`, which is what the year variable is named in the inflation dataset we created earlier.

```{stata echo = c(3), results="hide"}
* Join the inflation dataset with the SHEEO dataset ---------------

rename fy year
```

Join the inflation dataset with the SHEEO dataset

```{stata echo=TRUE}
save "data/higher_edu_data.dta", replace
```

```{stata echo=TRUE}
merge m:1 year using "data/inflation.dta"
```

# Data Analysis

Now that we have created a clean, merged dataset, we're almost ready to conduct our analysis.	But first, we need to examine our dataset to make sure everything looks like we expect it to.
We expect that:

1. Numeric variables don't have any missing or negative values.

2. There is an observation for each state and the U.S. total for each 
			year.
			
3. All values of `support` and `fte` are plausible compared to other values in the dataset (there are no outliers or impossible values).

Let's check these one by one.

1. Numeric variables don't have any missing or negative values.

The `inspect` and `misstable summarize` commands give information on missing values.

```{stata echo=TRUE}
inspect support fte cpi_u_rs_2019_adj
misstable summarize, all
```

2. There is an observation for each state and the U.S. total for each year.

First, confirm how many years are in our dataset.The `summarize` command gives summary statistics including the minimum and maximum.

```{stata echo=TRUE}
summarize year
```

But there might be gaps between the min and max. We need to see how many unique values of `year` are in the dataset. Let's use `inspect` again.
```{stata echo=TRUE}
inspect year
```

There are 40 years (1980 to 2019), so each state should have 40 observations. Let's create a frequency table to see.

```{stata echo=TRUE}
tabulate state
```

D.C has only 9 years of data. Let's see which years they are.

```{stata echo=TRUE}
tabulate year if state == "District of Columbia"
```

3. All values of `support` and `fte` are plausible compared to other values in the dataset (there are no outliers or impossible values).
Let's look at the 1st and 99th percentiles of each variable to check for outliers.

```{stata echo=TRUE}
summarize fte, detail
summarize support, detail
```

We're ready to construct our variable of interest. 

Adjust `support` for inflation by multiplying it by `cpi_u_rs`. 

```{stata echo=TRUE, results="hide"}
generate real_support = support * cpi_u_rs_2019_adj
```

Calculate real state support per FTE.

```{stata echo=TRUE, results="hide"}
generate real_support_per_fte = real_support / fte
```
	
```{stata echo=TRUE, results="hide"}
save "data/higher_edu_data.dta", replace
```

Our dataset has each year-state combination in a row. For presentation purposes, we might instead want a table where each state is a row, each year is a column, and the cells contain the value of `real_support_per_fte`. The `reshape` command can "pivot" or "transpose" a datset. 

```{stata echo=TRUE, results="hide"}
keep state year real_support_per_fte

reshape wide real_support_per_fte, i(state) j(year)
```

We want the names of each column in our table to be the year. But Stata's `reshape` command automatically prefixes the column names with the name of the variable in the cells. We can't rename the variables to the year, since Stata variables can't start with a number. So we will keep the variable names the way they are and put the year in the variable labels. Later, we will choose to export the table to Excel using the variable labels as the names of the columns.

We need to label all 40 year columns in the dataset. Instead of typing out 40 lines of code that label each column, we can use a *for* loop. A *for* loop works by repeating an action for all elements in a list. 
In this case, we can think of this as the loop writing the code for us.

```{stata echo=TRUE, results="hide"}
forvalues year = 1980/2019 {
    label variable real_support_per_fte`year' "`year'"
}
```

The first iteration of this loop will run:

```{stata eval=FALSE, results="hide"}
label variable real_support_per_fte1980 "1980"
```

The second iteration will run:

```{stata eval=FALSE, results="hide"}
label variable real_support_per_fte1981 "1981"
```

and so on.

As the last step, we export the table:

```{stata echo=TRUE, results="hide"}
export excel using "results/real_support_per_fte.xlsx",	///
	   firstrow(varlabels) replace
```


