---
title: "Exporting Stata Tables to Excel"
author: "Data Team, [Center on Budget and Policy Priorities](https://www.cbpp.org)"
date: "Spring 2021"
output: 
  html_document:
    theme: paper
    toc: true
    toc_depth: 1
---

```{r initialize-statamarkdown, echo=FALSE, message=FALSE}
library(Statamarkdown)
```

```{r setup, include=FALSE}
knitr::opts_chunk$set(collectcode=TRUE, 
                      # comment out the line below to execute Stata code
                      eval=FALSE, 
                      savedo=TRUE)
options(knitr.kable.NA = "")
library(tidyverse)
library(readxl)
library(kableExtra)
```

```{stata render-setup, include=FALSE, savedo=FALSE}
set rmsg off
set more off
set linesize 120
```


# Setup

Download the [code](https://raw.githubusercontent.com/CenterOnBudget/stata-trainings/master/stata-tables-to-excel/export_stata_tables_to_excel.do) to follow along in Stata.

In this tutorial, we'll be using a dataset containing penguin measurements
collected by scientists at Palmer Station, Antarctica.

```{stata, echo = c(-1:-2)}
* Load data

use "https://github.com/CenterOnBudget/stata-trainings/raw/master/penguins-dta/penguins.dta", clear
```


# Motivation

Stata has many options for creating frequency tables and tables of summary statistics. Many of these commands, however, don't facilitate afterwards getting the tables they display out of Stata and into an Excel spreadsheet in a reproducible way. For instance, the only way users can get a table produced by `tabulate` out of Stata is to copy-paste the table from the Results pane into a spreadsheet.

That may seem easy enough to do once, but keep in mind that in a real-world analysis, we'd likely want multiple tables of results in a spreadsheet, and may change what we're calculating and how as we refine our analysis. With a manual copy-paste approach, we'd need to keep track of which tables changed as we revise our `.do` file, and replace each one by scrolling through the Results pane and copy-pasting again. This runs the risk of human error, like typos or pasting the wrong table to the wrong place in a spreadsheet, or neglecting to re-paste in a table that’s changed.

Thankfully, Stata also has commands for summary statistics that _do_ provide a way to subsequently write the content of those tables to Excel. This tutorial will demonstrate that when we use these commands, when we re-run our `.do` file, the Excel spreadsheet will be automatically populated with our tables &mdash; no copy-pasting needed.

This tutorial is not intended for users working with survey data with a complex survey design, such as American Community Survey or Current Population Survey microdata. If you are analyzing survey data, we highly recommend using Stata's [survey estimation](https://www.stata.com/manuals/svysvyestimation.pdf#svysvyestimation) commands rather than the commands covered in this tutorial. With survey estimation commands, you can correctly calculate standard errors with which to gauge the reliability of your estimates and can conduct statistical tests to ensure that observed differences are statistically significant. A tutorial on survey data analysis with Stata can be found [here](https://centeronbudget.github.io/stata-tranings).


# Complementary commands

Before we dive in, we first need to introduce a few other commands we will use.

For the most part, the commands we'll be using work by replacing the data in memory with the frequency table or table of summary statistics. Because we want to keep working with our original dataset, we'll sandwich most of our work between the commands `preserve` and `restore`. These commands do exactly what their names suggest. After the data in memory has been replaced with the table, we'll use the `export excel` command to export the dataset as a sheet in an Excel workbook.

In one case, the command does not replace data in memory, but instead stores the components of the table in a special place in Stata's memory known as "returned results" (more on that later). After using that command, we'll use `putexcel set` to indicate which Excel workbook and sheet we’d like the results to be written to. Then, we'll use `putexcel` to place the results into the spreadsheet. The `putexcel` command requires Stata version 13 or later.

In this tutorial, we'll be placing each table in its own new worksheet in the top-left cell. It's worth noting, however, that both `export excel` and `putexcel` allow users to place multiple tables or other output in the same worksheet by modifying the sheet rather than replacing it, and specifying in which cell the commands should place output. (See the [bonus section](#bonus) for examples.) And while this tutorial won't cover cell formatting, `putexcel` has many options to modify cell formatting, and `export excel`'s `keepcellfmt` option can preserve existing cell formatting. 


```{stata, eval=FALSE, include=FALSE, collectcode=FALSE, echo=c(-1:-2)}
* Complementary commands

help preserve

help export excel

help putexcel
``````


# Frequency tables

## contract

`contract` replaces the dataset in memory with a new dataset consisting of all combinations of a categorical variable(s) that exist in the data and a new variable that contains the frequency of each combination. 

In the new dataset, missings indicate observations with missing values in the categorical variable(s). To exclude observations with missing values, specify the `nomiss` option. 

### Syntax

`contract varlist` where `varlist` is the categorical variable or combination of categorical variables for which frequencies will be tabulated.

Key options

- `freq()` the name of the frequency variable to be created; the default is "_freq".
- `percent()` create a percent variable and specify the name of the percent variable. 
- `zero` include combinations with zero frequencies.
- `nomiss` drop observations with missing values.

### Example

```{stata, eval=FALSE, include=FALSE, collectcode=FALSE, echo=c(-1:-2)}
* Frequency tables: contract

help contract
```

```{stata, results="hide"}
preserve
contract species island sex, freq(n_obs) percent(pct_obs) 
export excel "penguin_measures.xlsx", firstrow(variables) sheet("freq_contract", replace) 
restore
```

```{r, echo=FALSE, message=FALSE}
read_excel("penguin_measures.xlsx", sheet = "freq_contract") %>%
  kbl() %>%
  kable_styling(full_width = FALSE, position = "left")
```

## table, replace

The `table` command is a highly flexible tool for frequency tables and summary statistics. This section will cover how to use `table` with the `replace` option to replace the data in memory with new dataset consisting of all combinations of a categorical variable(s) that exist in the data, a new variable that contains the frequency of each combination, and, optionally, sub-totals if a combination of categorical variables is specified (a "two-way" or "three-way" table).[^1]

[^1]: Users may obtain higher-dimensional tables by also specifying up to four variables to `by()`. Examples can be found in the [`table` entry in the Stata Reference Manual](https://www.stata.com/manuals/rtable.pdf).

Unlike `contract`, observations with missing values in the categorical variable(s) are dropped. In the new dataset, missings indicate sub-totals, not missing values (sub-totals only appear if `row`, `column`, and/or `scolumn` are specified).[^2]

[^2]: Users may replace missings with a more informative value, such as "Subtotal", for each categorical variable over which frequencies were tabulated, prior to exporting to Excel. For variables with value labels, first replace missings (which cannot be labeled), with an [extended missing value](https://www.stata.com/manuals/u12.pdf#page=3) such as .m. Then, add a label for .m to the variable's existing value label. In the example:
```{.stata}
foreach var of varlist species island sex {
  replace `var' = .m if missing(`var')
  label define `var' .m  "Subtotal", add
}
```
For string variables, simply replace the missings. In the example, imagining if species, island, and sex were string variables:
```{.stata}
foreach var of varlist species island sex {
  replace `var' = "Subtotal" if `var' == ""
}
```

### Tips

- Users will likely wish to re-sort and re-order the new dataset so that categories are in an intuitive order.
- Users will likely wish to re-name the frequency variable name ("table1") before exporting to Excel, or to write the frequency variable label ("Freq."), rather than the variable name to Excel by specifying the `firstrow(varlabels)` option to `export excel`.

### Syntax

`table varlist` where `varlist` is the categorical variable or combination of categorical variables for which frequencies will be tabulated.

Key options

- `row` include row sub-totals (must specify at least one variable to `varlist`).
- `column` include column sub-totals (must specify at least two variables to `varlist`).
- `scolumn` include "supercolumn" sub-totals (must specify three variables to `varlist`).

### Example

```{stata, eval=FALSE, include=FALSE, collectcode=FALSE, echo=c(-1:-2)}
* Frequency tables: table, replace

help table
```

```{stata, results="hide"}
preserve
table sex island species, row column scolumn replace
order species island sex 
sort species island sex
export excel "penguin_measures.xlsx", firstrow(varlabels) sheet("freq_table", replace)
restore
```

```{r, echo=FALSE, message=FALSE}
read_excel("penguin_measures.xlsx", sheet = "freq_table") %>%
  kbl() %>%
  kable_styling(full_width = FALSE, position = "left")
```


## xtable

The `xtable` command is almost identical to `table`, except that it exports the table output to an Excel spreadsheet almost exactly how it appears in the Results window. 

`xtable` is a user-contributed command, so we need to install it first.

```{stata, results="hide", echo=c(-1:-2)}
* Frequency tables: xtable

ssc install xtable
```

### Syntax

The syntax is the same as `table`, with the following additional options:[^3]

- `filename()` the name of the Excel workbook to create or modify.
- `sheet()` the worksheet of the Excel workbook. Specify `sheet("sheetname", replace)` to replace the sheet if it already exists.
- `replace` to replace the Excel workbook if it already exists.
- `modify` to modify the workbook instead of replacing it.

[^3]: There is also a `noput` option that prevents `xtable` from writing to the worksheet. See the `xtable` help file for information on why this option is useful.

### Example

```{stata, eval=FALSE, include=FALSE, collectcode=FALSE}
help xtable
```

```{stata, results="hide"}
xtable sex island species, filename("penguin_measures.xlsx") modify sheet("freq_xtable", replace)
```

```{r, echo=FALSE, message=FALSE}
read_excel("penguin_measures.xlsx", sheet = "freq_xtable", skip = 1,
           .name_repair = "minimal") %>%
  kbl(caption = "species and island") %>%
  kable_styling(full_width = FALSE, position = "left")
```


# Tables of summary statistics

## collapse

The `collapse` command replaces the dataset in memory with a dataset of summary statistics. Available statistics include mean, median, min, max, sum, and count. 

### Syntax

`collapse (statistic) newvar = varname` where `statistic` is the statistic to be computed, `newvar` is the name of the variable to be created, and `varname` is the existing variable for which the statistic will be computed. For instance, `collapse (mean) mean_bill_length = bill_length_mm` will replace the data in memory with a new variable, mean_bill_length, containing the mean of bill_length_mm. 

Users may specify the `by()` option to calculate statistics by a categorical variable or combination of categorical variables. In the new dataset, missings indicate observations with missing values in the categorical variable(s).

### Example

```{stata, eval=FALSE, include=FALSE, collectcode=FALSE, echo=c(-1:-2)}
* Tables of summary statistics: collapse

help collapse
```

```{stata, results="hide"}
preserve
collapse (count)  n_obs_bill_length = bill_length_mm    ///
         (mean)   mean_bill_length  = bill_length_mm    ///
         (median) med_bill_length   = bill_length_mm    ///
         (count)  n_obs_bill_depth  = bill_depth_mm     ///
         (mean)   mean_bill_depth   = bill_depth_mm     ///
         (median) med_bill_depth    = bill_depth_mm,    ///
         by(species island sex)
export excel "penguin_measures.xlsx", firstrow(variables) sheet("stats_collapse", replace)
restore
```

```{r, echo=FALSE, message=FALSE}
read_excel("penguin_measures.xlsx", sheet = "stats_collapse") %>%
  kbl(digits = 3) %>%
  kable_styling(full_width = FALSE, position = "left")
```


## table, replace

The `table` command is a highly flexible tool for frequency tables and summary statistics. This section will cover how to use `table` with the `replace` option to replace the data in memory with new dataset consisting of up to five summary statistics, optionally by a categorical variable or combination of categorical variables

Unlike `collapse`, observations with missing values in the categorical variable(s) are dropped. In the new dataset, missings indicate overall statistics, not missing values (overall statistics only appear if `row`, `column`, and/or `scolumn` are specified).[^4] 

[^4]: See note 2.

### Syntax

`table varlist, contents()` where:

- `varlist` is the categorical variable or combination of categorical variables over which statistics will be computed. Up to three variables may be specified.[^5]
- `contents()` is a list of up to five statistics to be computed. The syntax for `contents()` is `contents(statistic varname)` where `statistic` is the statistic and `varname` is the existing variable for which the statistic will be computed. 

[^5]: See note 1.

Key options

- `row` include row overall statistics (must specify at least one variable to `varlist`).
- `column` include column overall statistics (must specify at least two variables to `varlist`).
- `scolumn` include "supercolumn" overall statistics (must specify three variables to `varlist`).
- `cw` if statistics on multiple variables are requested, use only observations with non-missing values in all of the variables in the calculations. Otherwise, all observations possible are used to calculate each of the specified statistics.

### Tips

- Users will likely wish to re-sort and re-order the new dataset so that categories are in an intuitive order.
- Users will likely wish to re-name the statistic variable names ("table1", "table2", etc.) before exporting to Excel, or to write the variable labels, rather than the variable name to Excel by specifying the `firstrow(varlabels)` option to `export excel`. Note that the variable labels may be truncated.

### Example

```{stata, eval=FALSE, include=FALSE, collectcode=FALSE, echo=c(-1:-2)}
* Tables of summary statistics: table, replace

help table
```

```{stata, results="hide"}
preserve
table sex island species, row column scolumn replace        ///
                          contents(mean    bill_length_mm   ///
                                   median  bill_length_mm   ///
                                   mean    bill_depth_mm    ///
                                   median  bill_depth_mm)
rename (table1 table2 table3 table4)                        ///
       (mean_bill_length med_bill_length mean_bill_depth med_bill_depth)
order species island sex 
sort species island sex
export excel "penguin_measures.xlsx", firstrow(variables) sheet("stats_table", replace)
restore
```

```{r, echo=FALSE, message=FALSE}
read_excel("penguin_measures.xlsx", sheet = "stats_table") %>%
  kbl(digits = 3) %>%
  kable_styling(full_width = FALSE, position = "left")
```


## xtable

The `xtable` command is almost identical to `table`, except that it exports the table output to an Excel spreadsheet almost exactly how it appears in the Results window. Unfortunately, `xtable` does not provide a way to export the names of the statistics, so it is most useful when only one statistic is specified to `contents()`. 

### Syntax

The syntax is the same as `table`, with the following additional options:[^6]

- `filename()` the name of the Excel workbook to create or modify.
- `sheet()` the worksheet of the Excel workbook. Specify `sheet("sheetname", replace)` to replace the sheet if it already exists.
- `replace` to replace the Excel workbook if it already exists.
- `modify` to modify the workbook instead of replacing it.

[^6]: See note 3.

### Example

```{stata, eval=FALSE, include=FALSE, collectcode=FALSE, echo=c(-1:-2)}
* Tables of summary statistics: xtable

help xtable
```

```{stata, results="hide"}
xtable sex island species, contents(mean bill_length_mm)              ///
                           filename("penguin_measures.xlsx") modify   ///
                           sheet("stats_xtable", replace)
```

```{r, echo=FALSE, message=FALSE}
df <- read_excel("penguin_measures.xlsx", sheet = "stats_xtable", skip = 1, 
                   .name_repair = "minimal") 
cols <- colnames(df)
df <- read_excel("penguin_measures.xlsx", sheet = "stats_xtable", skip = 1) 
df <- bind_rows(
  slice(df, 1), 
  slice(df, -1) %>%
    mutate(across(c(2:10), 
                  ~ as.numeric(.) %>% round(., 3) %>% as.character(.)))) %>%
  set_names(cols) 
df %>%
  kbl(caption = "species and island") %>%
  kable_styling(full_width = FALSE, position = "left")
```


## tabstat, save and tabstatmat

The `tabstat` command produces a table of summary statistics. With the `save` option, these contents of the table are saved in a special place in Stata's memory known as "returned results". Users can gather the returned results into a matrix with the `tabstatmat` command, and write the returned results to an Excel spreadsheet with the `putexcel` command.

### Syntax

`tabstat varlist, statistics()` where `varlist` is the variable or variables for which statistics will be computed and `statistics` is the statistics to be computed. Unlike `table`, there is no limit on the number of statistics to be computed.

Users may specify the `by()` option to calculate statistics by a categorical variable. Only one variable may be specified. If `by()` is specified, overall statistics will be included unless `nototal` is specified. Observations with missing values in the categorical variable are dropped unless `missing` is specified.

Key options

- `columns()` specify whether to display variables or statistics in the columns of the table. This option only affects the table printed to the Results pane, not the layout of the returned results.
- `casewise` if statistics on multiple variables are requested, use only observations with non-missing values in all of the variables in the calculations. Otherwise, all observations possible are used to calculate each of the specified statistics.
- `missing` include observations with missing values in the `by()` variable.
- `nototal` do not include overall statistics (only for use with `by()`).

### Example

```{stata, eval=FALSE, include=FALSE, collectcode=FALSE, echo=c(-1:-2)}
* Tables of summary statistics: tabstat, save

help tabstat
```

```{stata, results="hide"}
tabstat bill_depth_mm bill_length_mm flipper_length_mm body_mass_g,        ///
        statistics(n mean median min max)                                  ///
        by(species) columns(statistics) save
```

We use the `tabstatmat` command to gather the returned results that `tabstat` has saved into a single matrix, which we'll name statistics_by_species. (It's not important to know much about Stata matrices in this context).[^7] `tabstatmat` is a user-contributed Stata command, so we need to install it first.

[^7]: Interested users can check out [Chapter 14, Matrix expressions](https://www.stata.com/manuals/u14.pdf) in the Stata User's Guide.

```{stata, results="hide"}
ssc install tabstatmat
tabstatmat statistics_by_species
```

Now we can use the `putexcel` command to export the matrix statistics_by_species to Excel. First, we indicate which Excel workbook and sheet we’d like the results to be written to with the `putexcel set` command . Then, we use `putexcel` to write statistics_by_species to the spreadsheet, using the `names` option to indicate that we want the matrix's row and column names to be exported.

```{stata, results="hide"}
putexcel set "penguin_measures.xlsx", modify sheet("stats_tabstat", replace)
putexcel A1 = matrix(statistics_by_species), names
```

```{r, echo=FALSE, message=FALSE}
read_excel("penguin_measures.xlsx", sheet = "stats_tabstat",
           .name_repair = "minimal") %>%
  kbl(digits = 3) %>%
  kable_styling(full_width = FALSE, position = "left")
```


# Bonus: Multiple tables per worksheet {#bonus}

In this tutorial, we've placed each table in its own new worksheet in the top-left cell. Here's an example of exporting multiple tables to the same worksheet by making use of `export excel`'s  `sheet("sheetname", `__`modify`__`)` and `cell()` options. The `putexcel` command never overwrites an existing worksheet unless the option `sheet("sheetname", `__`replace`__`)` is specified, as was done throughout the tutorial. By omitting __`, replace`__ from `sheet()`, `putexcel` will modify rather than replace the sheet.

```{stata, results="hide", echo=c(-1:-2)}
* Bonus: Multiple tables per worksheet

* first table
preserve
contract species sex, freq(n_obs) percent(pct_obs) 
export excel "penguin_measures.xlsx", firstrow(variables)                 ///
                                      sheet("bonus", modify) cell("A2")
restore

* second table
preserve
contract species island, freq(n_obs) percent(pct_obs) 
export excel "penguin_measures.xlsx", firstrow(variables)                 ///
                                      sheet("bonus", modify) cell("F2")
restore

* third table
tabstat bill_depth_mm bill_length_mm,          ///
        statistics(n mean median min max)      ///
        by(species) columns(statistics) save
tabstatmat statistics_by_species2
putexcel set "penguin_measures.xlsx", modify sheet("bonus")
putexcel A14 = matrix(statistics_by_species), names

```



